;回溯嘗試標籤
bgm:none -enter=2000;
playEffect:future.mp3 -volume=50;
changeBg: -next;
: -notend;
setTextbox:hide;
changeBg:bg school_gate_sunset.png -next;
setTransform:{"oldFilm":0,"reflectionFilm":0,"glitchFilm":1,"rgbFilm":1,"godrayFilm":0} -target=bg-main -duration=5000 -ease=circInOut;
changeBg:none;
changeBg:bg school_gate_sunset.png -next;
bgm:gentle_healing_piano.mp3 -enter=2000 -volume=50;
setTextbox:on;
小花:阿宅！你在發什麼呆阿～;
changeFigure:hua_happy1.png;
:夕陽的餘暉灑在校門口，|小花正笑著朝我走來...;
changeFigure:none -next;
changeFigure:hua_tea1.png -transform={"position":{"y":134},"scale":{"x":1.451,"y":1.402}};
小花:拿去！下次換你請我囉！;
我:小花...我...;
:你伸出手，快要碰到那瓶冰涼奶茶的瞬間...;
bgm:none;
playEffect:system_glitch.mp3;
setTransition: -exit=glitchFilm -target=bg-main;
setTransition: -exit=glitchFilm -target=fig-center;
changeBg:bg school_gate_bad.png;
changeFigure:hua_tea1_bad.png;
我:？？？|小花...小花！ -fontSize=large;
:你伸出手想抓住眼前的一切...;
: -notend;
setTextbox:hide;
changeBg:none;
changeFigure:none;
bgm:Canon.mp3 -enter=3000 -volume=50;
changeBg:bg bedroom_night_dark.png -next;
setTextbox:on;
:現實中，沒有撤回鍵。|那些做過的選擇，無法時光倒流重新來過。;
:這或許很殘酷，但我們必須面對現實：|『人生，沒有重來鍵。』;
我:（我看著自己顫抖的雙手。）|（那些以為是玩笑的決定，現在卻成了沉重的代價。）;
:孩子，別害怕。雖然過去無法改變，|但現在開始『止血』，一切還來得及。;
:承認錯誤並不可恥，|接下來的每一步都會很艱難，;
:但你，不需要，也不會是一個人獨自面對。;
我:（深深吸了一口氣）|對，我不能再逃避了。;
系統提示:【你要選擇哪一個『止血』的舉動？（這將是改變未來的開始）】;
choose:勇敢的一步：告訴可信賴的大人，尋求協助:04_003.txt|專業的支援：聯絡 iWIN 或衛福部性影像處理中心:04_004.txt;